
package BonusHandler
// Version 1.7 - Credits: Frotty, Overkane, muzzel, Crigges, Earth-Fury, Halithor
import AbilityObjEditing
import ObjectIds
import Preloader
import Icons

@configurable public constant MAX_BONUS_LIFE = 12
@configurable public constant MAX_BONUS_MANA = 12
@configurable public constant MAX_BONUS_DAMAGE = 10
@configurable public constant MAX_BONUS_ARMOR = 10
@configurable public constant MAX_BONUS_SIGHTRANGE = 11
@configurable public constant MAX_BONUS_LIFEREGEN = 4
@configurable public constant MAX_BONUS_MANAREGEN = 10
@configurable public constant MAX_BONUS_ATTACKSPEED = 9
@configurable public constant MAX_BONUS_STRENGTH = 9
@configurable public constant MAX_BONUS_AGILITY = 9
@configurable public constant MAX_BONUS_INTELLIGENCE = 9
@configurable public constant IS_MANAREGEN_PERCENTAL = false

int array powersOf2
int array rawShifts
int rawShiftNeg
BonusType array bonusTypes

public enum Bonus
	LIFE
	MANA
	DAMAGE
	ARMOR
	SIGHTRANGE
	LIFEREGEN
	MANAREGEN
	ATTACKSPEED
	STRENGTH
	AGILITY
	INTELLIGENCE

/** Gets the current bonus value */
public function unit.getBonus(Bonus bonusType) returns int
	let bonus = bonusTypes[bonusType castTo int]
	var result = 0
	if this.hasAbility(bonus.baseRaw + rawShiftNeg)
		result -= powersOf2[bonus.maxBonus]
	for i = 0 to bonus.maxBonus - 1
		result += this.hasAbility(bonus.baseRaw + shiftRaw(i)) ? powersOf2[i] : 0
	return result

/** Adds to current bonus bonus value */
public function unit.addBonus(Bonus bonusType, int bonusValue)
	let currentBonusValue = this.getBonus(bonusType)
	let afterBonusValue = currentBonusValue + bonusValue
	this.setBonus(bonusType, afterBonusValue)

/** Sets the bonus of specified type for the unit to the given amount. */
public function unit.setBonus(Bonus bonusType, int amount)
	bonusTypes[bonusType castTo int].setBonus(this, amount)

/** Sets the bonus of specified type for the unit to zero. */
public function unit.clearBonus(Bonus bonusType)
	bonusTypes[bonusType castTo int].clearBonus(this)

/** Gets the highest applicable value for the specified bonus type. */
public function Bonus.getMax() returns int
	return powersOf2[bonusTypes[this castTo int].maxBonus - 1]

/** Gets the smallest applicable value for the specified bonus type. */
public function Bonus.getMin() returns int
	return -powersOf2[bonusTypes[this castTo int].maxBonus]

/** Removes all bonuses from the specified unit. */
public function unit.clearAllBonuses()
	bonusTypes[Bonus.LIFE castTo int].clearBonus(this)
	bonusTypes[Bonus.MANA castTo int].clearBonus(this)
	bonusTypes[Bonus.DAMAGE castTo int].clearBonus(this)
	bonusTypes[Bonus.ARMOR castTo int].clearBonus(this)
	bonusTypes[Bonus.SIGHTRANGE castTo int].clearBonus(this)
	bonusTypes[Bonus.LIFEREGEN castTo int].clearBonus(this)
	bonusTypes[Bonus.MANAREGEN castTo int].clearBonus(this)
	bonusTypes[Bonus.ATTACKSPEED castTo int].clearBonus(this)
	bonusTypes[Bonus.STRENGTH castTo int].clearBonus(this)
	bonusTypes[Bonus.AGILITY castTo int].clearBonus(this)
	bonusTypes[Bonus.INTELLIGENCE castTo int].clearBonus(this)


/** Enumeration of the bonustypes. */
class BonusType
	int baseRaw
	int maxBonus

	construct(int baseRaw, int maxBonus)
		this.baseRaw = baseRaw
		this.maxBonus = maxBonus

	function setBonus(unit u, int bonus)
		if bonus >= powersOf2[maxBonus]
			Log.warn("Bonus exceeds maximum")
			return
		if bonus < -powersOf2[maxBonus]
			Log.warn("Bonus exceeds maximum")
			return

		int val
		bool addNeg = false
		var raw = baseRaw + rawShiftNeg
		if bonus < 0
			addNeg = true
			val = bonus + powersOf2[maxBonus]
		else
			val = bonus
			u..makeAbilityPermanent(raw, false)
			..removeAbility(raw)
		for n = maxBonus-1 downto 0
			raw = baseRaw + rawShifts[n]
			if val >= powersOf2[n]
				u..addAbility(raw)
				..makeAbilityPermanent(raw, true)
				val -= powersOf2[n]
			else
				u..makeAbilityPermanent(raw, false)
				..removeAbility(raw)
		// Add negative last to avoid setting stat to <0 (killing the unit, etc)
		if addNeg
			u..addAbility(baseRaw + rawShiftNeg)
			..makeAbilityPermanent(baseRaw + rawShiftNeg, true)

	function clearBonus(unit u)
		var raw = baseRaw + rawShiftNeg
		u.makeAbilityPermanent(raw, false)
		u.removeAbility(raw)
		for n = 0 to maxBonus-1
			raw = baseRaw + rawShifts[n]
			u.makeAbilityPermanent(raw, false)
			u.removeAbility(raw)


/** Example: 'XY00' + shiftRaw(15) -> 'XY15' */
function shiftRaw(int n) returns int
	if n < 10
		return n
	else
		return (n div 10) * 256 + n.moduloInt(10)


/** Example: pow2(x) -> 2^x */
function pow2(int e) returns int
	if e == 0
		return 1
	return 2 * pow2(e - 1)

function AbilityDefinition.setProperties(string suffix, string bonusName)
	this..setItemAbility(false)
	..setEditorSuffix("(" + suffix + ")")
	..setIconNormal(Icons.bTNPenguin)
	..setName("BonusHandler - " + bonusName)


init
	for n = 0 to 30 // if u need bonuses higher than 1 billion u should rethink your map design...
		powersOf2[n] = Pow(2, n.toReal()).toInt()
	for n = 0 to 30
		rawShifts[n] = shiftRaw(n)
	rawShiftNeg = '00--' - '0000'

	bonusTypes[Bonus.LIFE castTo int] = new BonusType('$L00', MAX_BONUS_LIFE)
	bonusTypes[Bonus.MANA castTo int] = new BonusType('$M00', MAX_BONUS_MANA)
	bonusTypes[Bonus.DAMAGE castTo int] = new BonusType('$D00', MAX_BONUS_DAMAGE)
	bonusTypes[Bonus.ARMOR castTo int] = new BonusType('$R00', MAX_BONUS_ARMOR)
	bonusTypes[Bonus.SIGHTRANGE castTo int] = new BonusType('$V00', MAX_BONUS_SIGHTRANGE)
	bonusTypes[Bonus.LIFEREGEN castTo int] = new BonusType('$l00', MAX_BONUS_LIFEREGEN)
	bonusTypes[Bonus.MANAREGEN castTo int] = new BonusType('$m00', MAX_BONUS_MANAREGEN)
	bonusTypes[Bonus.ATTACKSPEED castTo int] = new BonusType('$P00', MAX_BONUS_ATTACKSPEED)
	bonusTypes[Bonus.STRENGTH castTo int] = new BonusType('$S00', MAX_BONUS_STRENGTH)
	bonusTypes[Bonus.AGILITY castTo int] = new BonusType('$A00', MAX_BONUS_AGILITY)
	bonusTypes[Bonus.INTELLIGENCE castTo int] = new BonusType('$I00', MAX_BONUS_INTELLIGENCE)
	
	for int i = Bonus.LIFE castTo int to Bonus.INTELLIGENCE castTo int
		for int j = 0 to (bonusTypes[i].maxBonus - 1)
			preloadAbility(bonusTypes[i].baseRaw + shiftRaw(j))

@compiletime function genAbilities()
	string rawCode
	// Life:
	rawCode = "$L"
	for n = 0 to MAX_BONUS_LIFE
		string raw
		int sign = 1
		if n < MAX_BONUS_LIFE
			raw = rawCode
			if n < 10
				raw = raw + "0"
			raw = raw + (n).toString()
		else
			raw = rawCode + "--"
			sign = -1
		let a = new AbilityDefinitionMaxLifeBonusLeast(raw.fromRawCode())
		a.setMaxLifeGained(1, sign * pow2(n))
		a.setProperties((sign * pow2(n)).toString(), "Life")

	// Mana:
	rawCode = "$M"
	for n = 0 to MAX_BONUS_MANA
		string raw
		int sign = 1
		if n < MAX_BONUS_MANA
			raw = rawCode
			if n < 10
				raw = raw + "0"
			raw = raw + (n).toString()
		else
			raw = rawCode + "--"
			sign = -1
		let a = new AbilityDefinitionMaxManaBonusMost(raw.fromRawCode())
		a.setMaxManaGained(1, sign * pow2(n))
		a.setProperties((sign * pow2(n)).toString(), "Mana")
	// Damage:
	rawCode = "$D"
	for n = 0 to MAX_BONUS_DAMAGE
		string raw
		int sign = 1
		if n < MAX_BONUS_DAMAGE
			raw = rawCode
			if n < 10
				raw = raw + "0"
			raw = raw + (n).toString()
		else
			raw = rawCode + "--"
			sign = -1
		let a = new AbilityDefinitionAttackBonus(raw.fromRawCode())
		a.setAttackBonus(1, sign * pow2(n))
		a.setProperties((sign * pow2(n)).toString(), "Damage")

	// Armor:
	rawCode = "$R"
	for n = 0 to MAX_BONUS_ARMOR
		string raw
		int sign = 1
		if n < MAX_BONUS_ARMOR
			raw = rawCode
			if n < 10
				raw = raw + "0"
			raw = raw + (n).toString()
		else
			raw = rawCode + "--"
			sign = -1
		let a = new AbilityDefinitionDefenseBonusPlus1(raw.fromRawCode())
		a.setDefenseBonus(1, sign * pow2(n))
		a.setProperties((sign * pow2(n)).toString(), "Armor")

	// Sightrange:
	rawCode = "$V"
	for n = 0 to MAX_BONUS_SIGHTRANGE
		string raw
		int sign = 1
		if n < MAX_BONUS_SIGHTRANGE
			raw = rawCode
			if n < 10
				raw = raw + "0"
			raw = raw + (n).toString()
		else
			raw = rawCode + "--"
			sign = -1
		let a = new AbilityDefinitionSightBonus(raw.fromRawCode())
		a.setSightRangeBonus(1, sign * pow2(n))
		a.setProperties((sign * pow2(n)).toString(), "Sightrange")

	// Liferegen:
	rawCode = "$l"
	for n = 0 to MAX_BONUS_LIFEREGEN
		string raw
		int sign = 1
		if n < MAX_BONUS_LIFEREGEN
			raw = rawCode
			if n < 10
				raw = raw + "0"
			raw = raw + (n).toString()
		else
			raw = rawCode + "--"
			sign = -1
		let a = new AbilityDefinitionRegenLife(raw.fromRawCode())
		a.setHitPointsRegeneratedPerSecond(1, sign * pow2(n))
		a.setProperties((sign * pow2(n)).toString(), "Liferegen")

	// Manaregen:
	rawCode = "$m"
	for n = 0 to MAX_BONUS_MANAREGEN
		string raw
		int sign = 1
		if n < MAX_BONUS_MANAREGEN
			raw = rawCode
			if n < 10
				raw = raw + "0"
			raw = raw + (n).toString()
		else
			raw = rawCode + "--"
			sign = -1
		if IS_MANAREGEN_PERCENTAL
			let a = new AbilityDefinitionItemRegenMana(raw.fromRawCode())
			a.setManaRegenerationBonusasfractionofnormal(1, sign * pow2(n) / 100.)
			a.setProperties((sign * pow2(n)).toString() + "%", "Manaregen")
		else
			let a = new AbilityDefinitionNeutralRegenmanaonly(raw.fromRawCode())
			a.setPercentage(1, false)
			a.setAreaofEffect(1, 1)
			a.setTargetsAllowed(1, "self")
			a.setAmountRegenerated(1, sign * pow2(n) / 200.)
			a.setRace(Race.Other)
			a.setProperties((sign * pow2(n)).toString(), "Manaregen")

	// Attackspeed:
	rawCode = "$P"
	for n = 0 to MAX_BONUS_ATTACKSPEED
		string raw
		int sign = 1
		if n < MAX_BONUS_ATTACKSPEED
			raw = rawCode
			if n < 10
				raw = raw + "0"
			raw = raw + (n).toString()
		else
			raw = rawCode + "--"
			sign = -1
		let a = new AbilityDefinitionAttackSpeedIncrease(raw.fromRawCode())
		a.setAttackSpeedIncrease(1, sign * pow2(n) / 100.)
		a.setProperties((sign * pow2(n)).toString() + "%", "Attackspeed")

	// Strength:
	rawCode = "$S"
	for n = 0 to MAX_BONUS_STRENGTH
		string raw
		int sign = 1
		if n < MAX_BONUS_STRENGTH
			raw = rawCode
			if n < 10
				raw = raw + "0"
			raw = raw + (n).toString()
		else
			raw = rawCode + "--"
			sign = -1
		let a = new AbilityDefinitionStrengthBonusPlus1(raw.fromRawCode())
		a.setStrengthBonus(1, sign * pow2(n))
		a.setProperties((sign * pow2(n)).toString(), "Strength")

	// Agility:
	rawCode = "$A"
	for n = 0 to MAX_BONUS_AGILITY
		string raw
		int sign = 1
		if n < MAX_BONUS_AGILITY
			raw = rawCode
			if n < 10
				raw = raw + "0"
			raw = raw + (n).toString()
		else
			raw = rawCode + "--"
			sign = -1
		let a = new AbilityDefinitionAgilityBonusPlus1(raw.fromRawCode())
		a.setAgilityBonus(1, sign * pow2(n))
		a.setProperties((sign * pow2(n)).toString(), "Agility")

	// Intelligence:
	rawCode = "$I"
	for n = 0 to MAX_BONUS_INTELLIGENCE
		string raw
		int sign = 1
		if n < MAX_BONUS_INTELLIGENCE
			raw = rawCode
			if n < 10
				raw = raw + "0"
			raw = raw + (n).toString()
		else
			raw = rawCode + "--"
			sign = -1
		let a = new AbilityDefinitionIntelligenceBonusPlus1(raw.fromRawCode())
		a.setIntelligenceBonus(1, sign * pow2(n))
		a.setProperties((sign * pow2(n)).toString(), "Intelligence")

